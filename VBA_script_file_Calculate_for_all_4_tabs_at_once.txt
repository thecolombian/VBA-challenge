' Declare all the DIM at once so I can keep track of them '
Sub AnalyzeQuarterlyStockData()
    Dim ws As Worksheet
    Dim summaryWs As Worksheet
    Dim tickerCollection As New Collection
    Dim lastRow As Long
    Dim i As Long, j As Long
    Dim ticker As String
    Dim openPrice As Double, closePrice As Double
    Dim totalVolume As Double
    Dim quarterlyChange As Double, percentageChange As Double
    Dim uniqueTickers() As String
    Dim tickerCount As Long
    Dim item As Variant

    Dim maxIncrease As Double, maxDecrease As Double, maxVolume As Double
    Dim maxIncreaseTicker As String, maxDecreaseTicker As String, maxVolumeTicker As String

' lets get an array of the sheet TABS going - lets loop in case we need to add more tabs we can do it here'
    Dim sheetNames As Variant
    sheetNames = Array("Q1", "Q2", "Q3", "Q4")

    ' Loop through each sheet - KISS :)
    For Each sheetName In sheetNames
        Set ws = ThisWorkbook.Sheets(sheetName)
        Set summaryWs = ThisWorkbook.Sheets(sheetName)
        Set tickerCollection = New Collection

        ' Loop through the current worksheet to collect unique tickers - this kills my computer :(
        On Error Resume Next ' I want to catch my stupid typos :) '
        lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
        For i = 2 To lastRow
            ticker = ws.Cells(i, 1).Value
            tickerCollection.Add ticker, CStr(ticker)
        Next i
        On Error GoTo 0

        ' Convert the collection to an array for processing - so I do not have to count to 10
        tickerCount = tickerCollection.Count
        ReDim uniqueTickers(1 To tickerCount)
        j = 1
        For Each item In tickerCollection
            uniqueTickers(j) = item
            j = j + 1
        Next item

        ' Sort the array alphabetically - sounds easy :)
        Call QuickSort(uniqueTickers, LBound(uniqueTickers), UBound(uniqueTickers))

        ' Insert the label for the tickers column - using Cells
        summaryWs.Cells(1, 9).Value = "Ticker"
        summaryWs.Cells(1, 10).Value = "Quarterly Change"
        summaryWs.Cells(1, 11).Value = "Percentage Change"
        summaryWs.Cells(1, 12).Value = "Total Volume"

        ' Output the sorted tickers to the summary sheet
        For i = 1 To tickerCount
            summaryWs.Cells(i + 1, 9).Value = uniqueTickers(i)
        Next i

        ' Loop through each ticker to calculate metrics and keep them organized :)
        For i = 2 To tickerCount + 1
            ticker = summaryWs.Cells(i, 9).Value
            openPrice = 0
            closePrice = 0
            totalVolume = 0

            ' Loop through the current worksheet
            lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
            For j = 2 To lastRow
                If ws.Cells(j, 1).Value = ticker Then
                    If openPrice = 0 Then
                        openPrice = ws.Cells(j, 3).Value
                    End If
                    closePrice = ws.Cells(j, 6).Value
                    totalVolume = totalVolume + ws.Cells(j, 7).Value
                End If
            Next j

            ' Calculate quarterly change and percentage change
            quarterlyChange = closePrice - openPrice
            If openPrice <> 0 Then
                percentageChange = (quarterlyChange / openPrice) * 100
            Else
                percentageChange = 0
            End If

            ' Output results to summary sheet
            summaryWs.Cells(i, 10).Value = quarterlyChange
            summaryWs.Cells(i, 11).Value = percentageChange
            summaryWs.Cells(i, 12).Value = totalVolume

            ' Apply conditional formatting for Quarterly Change
            ' Yes I like RGB vs hard coded one single color - old school'
            If quarterlyChange > 0 Then
                summaryWs.Cells(i, 10).Interior.Color = RGB(144, 238, 144) ' Light green
            ElseIf quarterlyChange < 0 Then
                summaryWs.Cells(i, 10).Interior.Color = RGB(255, 182, 193) ' Light red
            End If

            ' Track maximum values
            If percentageChange > maxIncrease Then
                maxIncrease = percentageChange
                maxIncreaseTicker = ticker
            End If
            If percentageChange < maxDecrease Then
                maxDecrease = percentageChange
                maxDecreaseTicker = ticker
            End If
            If totalVolume > maxVolume Then
                maxVolume = totalVolume
                maxVolumeTicker = ticker
            End If
        Next i

        ' Output the results for maximum values
        summaryWs.Cells(1, 14).Value = "Greatest % Increase"
        summaryWs.Cells(2, 14).Value = maxIncreaseTicker
        summaryWs.Cells(2, 15).Value = maxIncrease

        summaryWs.Cells(4, 14).Value = "Greatest % Decrease"
        summaryWs.Cells(5, 14).Value = maxDecreaseTicker
        summaryWs.Cells(5, 15).Value = maxDecrease

        summaryWs.Cells(7, 14).Value = "Greatest Total Volume"
        summaryWs.Cells(8, 14).Value = maxVolumeTicker
        summaryWs.Cells(8, 15).Value = maxVolume
    Next sheetName
End Sub

' QuickSort algorithm for sorting the tickers array
' this was the only way I was able to get the correct Sort order '
Sub QuickSort(arr As Variant, first As Long, last As Long)
    Dim low As Long, high As Long
    Dim mid As Variant, temp As Variant

    low = first
    high = last
    mid = arr((first + last) \ 2)

    Do While low <= high
        Do While arr(low) < mid
            low = low + 1
        Loop
        Do While arr(high) > mid
            high = high - 1
        Loop
        If low <= high Then
            temp = arr(low)
            arr(low) = arr(high)
            arr(high) = temp
            low = low + 1
            high = high - 1
        End If
    Loop

    If first < high Then QuickSort arr, first, high
    If low < last Then QuickSort arr, low, last
End Sub
